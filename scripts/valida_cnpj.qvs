[CNPJ]:
Load
   CNPJ
  ,if( Len(CNPJ) = SubStringCount(CNPJ, Left(CNPJ, 1) ), 'Inválido'
  ,if( Right( CNPJ, 2 ) = Digito_1 & Digito_2, 'Válido', 'Inválido')) as Status_cnpj
;
Load
   CNPJ
  ,if( Digito_1 < 2, 0, 11 - Digito_1 ) as Digito_1
  ,if( Digito_2 < 2, 0, 11 - Digito_2 ) as Digito_2
;
Load
   CNPJ
  ,Digito_1
  ,Mod( RangeSum(
    Mid( CNPJ, 1, 1 ) * 6,
    Mid( CNPJ, 2, 1 ) * 5,
    Mid( CNPJ, 3, 1 ) * 4,
    Mid( CNPJ, 4, 1 ) * 3,
    Mid( CNPJ, 5, 1 ) * 2,
    Mid( CNPJ, 6, 1 ) * 9,
    Mid( CNPJ, 7, 1 ) * 8,
    Mid( CNPJ, 8, 1 ) * 7,
    Mid( CNPJ, 9, 1 ) * 6,
    Mid( CNPJ, 10, 1 ) * 5,
    Mid( CNPJ, 11, 1 ) * 4,
    Mid( CNPJ, 12, 1 ) * 3,
    if( Digito_1 < 2, 0, 11 - Digito_1 ) * 2
  ), 11 ) as Digito_2
;
Load
   CNPJ
  ,Mod( RangeSum(
    Mid( CNPJ, 1, 1 ) * 5,
    Mid( CNPJ, 2, 1 ) * 4,
    Mid( CNPJ, 3, 1 ) * 3,
    Mid( CNPJ, 4, 1 ) * 2,
    Mid( CNPJ, 5, 1 ) * 9,
    Mid( CNPJ, 6, 1 ) * 8,
    Mid( CNPJ, 7, 1 ) * 7,
    Mid( CNPJ, 8, 1 ) * 6,
    Mid( CNPJ, 9, 1 ) * 5,
    Mid( CNPJ, 10, 1 ) * 4,
    Mid( CNPJ, 11, 1 ) * 3,
    Mid( CNPJ, 12, 1 ) * 2
  ), 11 ) as Digito_1
;

//Removing unnecessary characters while keeping only 14-digit numbers.
load 
  Num( KeepChar(CNPJ, '0123456789'), '00000000000000' ) AS CNPJ
;

//This is only for example. Load CNPJs from your dataset.
load * inline [
	CNPJ
	03.571.070/0003-50
	03.571.112/0001-92
	03.571.070/0003-55
];